// è‡ªåŠ¨ç”Ÿæˆå¤´æ–‡ä»¶åˆ—è¡¨
#ifndef INCLUDE_DIR
#define INCLUDE_DIR "/include/"
#endif

string header = @LEAD
/** This program is a part of mudcore
 *-----------------------------------------
 * File   : %^FILENAME%^
 * Author : MUDCORE
 * Note   : Include Fileï¼Œç”± creator è‡ªåŠ¨ç”Ÿæˆ
 * Update : %^CTIME%^
 *-----------------------------------------
 */
LEAD;

int valid_file_name(string file)
{
    foreach (int a in file)
    {
        if (!(a >= '0' && a <= '9') && !(a >= 'A' && a <= 'Z') && !(a >= 'a' && a <= 'z') && a != '_')
            return 0;
    }

    return 1;
}

varargs string files(string *files, string prefix)
{
    string sf, file = "", pre = "";

    if (stringp(prefix))
    {
        pre = prefix + "_";
    }

    foreach (string f in files)
    {
        if (f[ < 2.. < 1] != ".c")
            continue;

        sf = f[strsrch(f, "/", -1) + 1.. < 3];

        if (valid_file_name(sf))
            file += sprintf("%s%-40s%s", "#define ", pre + upper_case(sf), " \"" + f + "\"\n");
    }

    return file;
}

varargs void create_include(string base_name, string dir, string prefix)
{
    string filename = "_" + base_name + ".h";
    string file = terminal_colour(header, (["FILENAME":filename, "CTIME":ctime()]));
    string *files = deep_path_list(dir);

    file += files(files, prefix);

    write_file(INCLUDE_DIR + filename, file, 1);
    write(INCLUDE_DIR + filename + " å·²ç”Ÿæˆ ðŸ’š\n");
}

void create_inherit_include()
{
#ifdef INHERIT_DIR
    create_include("inherit", INHERIT_DIR, "");
#endif
}

void create_daemon_include()
{
#ifdef DAEMON_DIR
    create_include("daemon", DAEMON_DIR);
#endif
}

void create_std_include()
{
#ifdef STD_DIR
    create_include("std", STD_DIR, "STD");
#endif
}

void create_all_include()
{
    // _inherit.h
    create_inherit_include();

    // _daemon.h
    create_daemon_include();

    // _std.h
    create_std_include();
}

void create()
{
    // create_all_include();
}

string short()
{
    return "å¤´æ–‡ä»¶è‡ªåŠ¨åŒ–ç³»ç»Ÿ(HEADER_D)";
}
